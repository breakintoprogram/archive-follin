ORG 60000
LOAD 49152
DI 
LD HL,ECTA
LD B,16
ECF1:LD (HL),85
INC HL
LD (HL),1
INC HL
LD (HL),31
INC HL
DJNZ ECF1
LD HL,TABLE
INSC:LD BC,65533
LD A,(HL)
INC HL
OUT (C),A
LD BC,49149
LD A,(HL)
INC HL
OUT (C),A
LD A,(HL)
AND A
JP NZ,INSC
LD IX,50000     ;CHORD
LD (CHREP),IX
LD IY,51000     ;BASS
LD A,1
LD (MELO+1),A
LD (SHAZ+1),A
LD (REPBA+1),A
LD (REPME+1),A
LD (REPCH+1),A
LD (PRT+1),A
LD (BAREP),IY
LD A,(INIM)
LD (INOM),A
LD A,(ATTAM)
LD (SISM+1),A
LD A,(DECAM)
LD (FIM+1),A
EXX 
LD DE,52000     ;MELODY
LD (MEREP),DE
EXX 
STAR:LD A,(IX+0)
CP 2
JP NZ,STT2
INC IX
LD A,(IX+0)
INC A
LD (REPCH+1),A
INC IX
LD (CHREP),IX
LD A,(IX+0)
JP STT3
STT2:CP 1
JP NZ,STT3
REPCH:LD A,001
DEC A
JP NZ,ZOK
INC IX
JP STAR
ZOK:LD (REPCH+1),A
LD IX,(CHREP)
LD A,(IX+0)
STT3:AND A
JP Z,BASIC
CP 255
JP NZ,NEXT
INC IX
INC IX
LD A,(IX-1)
DEC A
JP Z,ENVIN
DEC A
JP Z,BEATN
DEC A
JP Z,BEATF
DEC A
JP Z,BEA1
DEC A
JP Z,BEA2
DEC A
JP Z,BEA3
DEC A
JP Z,BEA4
DEC A
JP Z,BEASP
DEC A
JP Z,ECON
DEC A
JP Z,ECOF
DEC A
JP Z,WAIS
JP STAR
ECON:LD A,1
LD (ECMI+1),A
LD (ECHO+1),A
JP STAR
ECOF:XOR A
LD (ECMI+1),A
LD (ECHO+1),A
JP STAR
WAIS:LD A,(IX+0)
INC IX
LD (ROC1+1),A
LD (ROC2+1),A
JP STAR
BEASP:LD A,(IX+0)
INC IX
LD (BSP+1),A
JP STAR
BEA1:LD HL,TAB1
LD (BTAP+1),HL
JP BEATN
BEA2:LD HL,TAB2
LD (BTAP+1),HL
JP BEATN
BEA3:LD HL,TAB3
LD (BTAP+1),HL
JP BEATN
BEA4:LD HL,TAB4
LD (BTAP+1),HL
JP BEATN
BEATN:XOR A
LD (EAD+1),A
LD A,1
LD (BECO+1),A
JP STAR
BEATF:XOR A
LD (EAD+1),A
XOR A
LD (BECO+1),A
JP STAR
ENVIN:LD A,(IX+0)
LD (ININ),A
LD (INOU),A
LD A,(IX+1)
LD (ATTAK),A
LD A,(IX+2)
LD (DECAY),A
LD A,(IX+3)
LD (SUS+1),A
INC IX
INC IX
INC IX
INC IX
JP STAR
NEXT:LD A,(ININ)
LD (INOU),A
LD A,(ATTAK)
LD (SISP+1),A
LD A,(DECAY)
LD (FIN+1),A
LD E,(IX+0)
LD H,(IX+1)
INC IX
INC IX
ECMI:LD A,000
DEC A
JP Z,EC2
LD D,(IX+0)
INC IX
EC2:LD A,(INOU)
AND A
JP Z,FINIT
LD A,D
LD (SMCD+1),A
SRL A
SRL A
SRL A
LD (SDS+1),A
LD (CPD+1),A
LD A,E
LD (SMCE+1),A
SRL A
SRL A
SRL A
LD (SES+1),A
LD (CPE+1),A
LD A,H
LD (SMCH+1),A
SRL A
SRL A
SRL A
LD (SHS+1),A
LD (CPH+1),A
LD A,1
LD (SMXD+1),A
LD (SMXE+1),A
LD (SMXH+1),A
IRT:LD A,0
LD (ND4+1),A
LD A,(IX+0)
INC IX
POT:EX AF,AF
LD A,07FH
IN A,(254)
AND 1
JP Z,BASIC
WUPS:LD A,000
INC A
LD (WUPS+1),A
WANU:CP 1
JP NZ,JOBS
XOR A
LD (WUPS+1),A
BROC:LD B,000
BCOR:LD A,(SMMCL+1)
DEC A
JP Z,JOBS
LD (SMMCL+1),A
LD A,(SMFL+1)
INC A
LD (SMFL+1),A
DJNZ BCOR
JOBS:LD A,(INOM)
AND A
JP Z,FIM
SISM:LD A,000
DEC A
LD (SISM+1),A
JP NZ,AMP
LD A,(ATTAM)
LD (SISM+1),A
MROC1:LD B,000
MCOR1:LD A,(SMXC+1)
INC A
CPC:CP 000
JP Z,FLIM
LD (SMXC+1),A
LD A,(SCS+1)
DEC A
LD (SCS+1),A
DJNZ MCOR1
JP AMP
FIM:LD A,000
DEC A
LD (FIM+1),A
JP NZ,AMP
LD A,(DECAM)
LD (FIM+1),A
MROC2:LD B,000
MCOR2:LD A,(SMXC+1)
DEC A
JP Z,AMP
LD (SMXC+1),A
LD A,(SCS+1)
INC A
LD (SCS+1),A
DJNZ MCOR2
AMP:LD A,(OLNO+1)
LD B,A
LD A,(COMP+1)
CP B
JP Z,BECO
PRS:LD B,4
OLNO:LD A,000
COMP:CP 000
JP Z,FIX
JP C,GUP
DEC A
DEC A
GUP:INC A
LD (OLNO+1),A
DJNZ OLNO
FIX:LD B,A
CALL SORT
BECO:LD A,000
DEC A
CP 255
JP Z,SHAZ
LD (BECO+1),A
AND A
JP NZ,SHAZ
PUSH HL
PUSH DE
PUSH BC
BTAP:LD HL,TAB1
LD D,0
EAD:LD E,000
ADD HL,DE
LD A,E
ADD 2
LD (EAD+1),A
LD A,(HL)
LD C,A
BSP:LD B,003
DEC B
JP Z,NOK
OK:ADD C
DJNZ OK
NOK:LD (BECO+1),A
INC HL
PUSH HL
LD A,(HL)
INC A
DEC A
CALL Z,BASS
DEC A
CALL Z,TINK
DEC A
CALL Z,SNAR
DEC A
CALL Z,HIGH
DEC A
CALL Z,FONK
POP HL
INC HL
LD A,(HL)
CP 255
JP NZ,DOK
XOR A
LD (EAD+1),A
DOK:POP BC
POP DE
POP HL
SHAZ:LD A,000
DEC A
LD (SHAZ+1),A
JP NZ,MELO
IZI:LD A,(IY+0)
CP 2
JP NZ,JJHD
INC IY
LD A,(IY+0)
INC A
LD (REPBA+1),A
INC IY
LD (BAREP),IY
JP IZI
JJHD:CP 1
JP NZ,HAHA
REPBA:LD A,001
DEC A
JP Z,TIF
LD (REPBA+1),A
LD IY,(BAREP)
JP IZI
TIF:INC IY
JP IZI
HAHA:CP 3
JP NZ,SNIAP
INC IY
LD A,(IY+0)
LD (WANU+1),A
DEC A
LD (WUPS+1),A
INC IY
JP IZI
SNIAP:CP 4
JP NZ,PAINS
INC IY
LD A,(IY+0)
LD (BROC+1),A
INC IY
JP IZI
PAINS:LD (SMCL+1),A
SRL A
LD (SMMCL+1),A
LD A,1
LD (SMFL+1),A
LD A,(IY+1)
LD (SHAZ+1),A
INC IY
INC IY
MELO:LD A,000
DEC A
LD (MELO+1),A
JP NZ,ECHO
LD A,1
LD (SISM+1),A
LD (FIM+1),A
LD A,(INIM)
LD (INOM),A
EXX 
JSH:LD A,(DE)
CP 2
JP NZ,FITF
INC DE
LD A,(DE)
INC A
LD (REPME+1),A
INC DE
LD (MEREP),DE
JP JSH
FITF:CP 1
JP NZ,NTFF
REPME:LD A,001
DEC A
JP Z,HSJ
LD (REPME+1),A
LD DE,(MEREP)
JP JSH
HSJ:INC DE
JP JSH
NTFF:LD A,(DE)
CP 3
JP NZ,FITN
INC DE
LD A,(DE)
INC DE
DEC A
JP Z,PORON
DEC A
JP Z,POROF
DEC A
JP Z,PORSP
DEC A
JP Z,MEEN
DEC A
JP Z,SDFE
DEC A
JP Z,SOC1
DEC A
JP Z,TOBY
DEC A
JP Z,VIDEL
DEC A
JP Z,VITI
JP JSH
TOBY:LD A,(DE)
LD (MROC1+1),A
LD (MROC2+1),A
INC DE
JP JSH
VITI:LD A,(DE)
LD (VISP+1),A
LD A,1
LD (VIPS+1),A
INC DE
PUSH HL
PUSH BC
EX DE,HL
LD BC,8
LD DE,VIBTA
LDIR 
EX DE,HL
POP BC
POP HL
JP JSH
VIDEL:LD A,(DE)
INC DE
LD (IVA+1),A
JP JSH
SOC1:LD A,(DE)
LD (CHOR+1),A
INC DE
JP JSH
SDFE:LD A,(DE)
INC DE
LD (DELAY+1),A
JP JSH
PORSP:LD A,(DE)
LD (PRS+1),A
INC DE
JP JSH
PORON:LD A,1
LD (PRT+1),A
JP JSH
POROF:XOR A
LD (PRT+1),A
JP JSH
MEEN:LD A,(DE)
LD (INIM),A
LD (INOM),A
INC DE
LD A,(DE)
LD (ATTAM),A
INC DE
LD A,(DE)
LD (DECAM),A
INC DE
LD A,1
LD (SISM+1),A
LD (FIM+1),A
JP JSH
FITN:INC DE
LD (COMP+1),A
LD B,A
PRT:LD A,000
AND A
JP NZ,NOPO
LD A,B
EXX 
LD L,A
EXX 
CALL SORT
LD A,(SMCC+1)
LD (OLNO+1),A
NOPO:LD A,(DE)
LD (MELO+1),A
INC DE
EXX 
ECHO:LD A,000
DEC A
JR NZ,OHWELL
PUSH HL
PUSH BC
POINT:LD A,000
INC A
AND 15
LD (POINT+1),A
LD HL,ECTA
LD C,A
ADD A
ADD C
LD C,A
LD B,0
ADD HL,BC
LD A,(SMCC+1)
LD (HL),A
INC HL
LD A,(SMXC+1)
LD (HL),A
INC HL
LD A,(SCS+1)
LD (HL),A
LD A,(POINT+1)
DELAY:SUB 015
AND 15
LD HL,ECTA
LD C,A
ADD A
ADD C
LD C,A
ADD HL,BC
LD A,(HL)
CHOR:SUB 000
LD (SMCD+1),A
INC HL
LD A,(HL)
SRL A
LD B,A
SRL A
SRL A
SRL A
ADD B
OR 1
LD (SMXD+1),A
INC HL
LD A,(HL)
SRL A
LD B,A
SRL A
SRL A
SRL A
ADD B
OR 1
LD (SDS+1),A
POP BC
POP HL
OHWELL:CALL KOP
LD A,07FH
IN A,(254)
AND 1
JP Z,BASIC
VIB:LD A,000
DEC A
LD (VIB+1),A
JP NZ,TDKBO
LD A,(SMCC+1)
CP 254
JP Z,TDKBO
LD A,1
LD (VIB+1),A
VIPS:LD A,001
DEC A
LD (VIPS+1),A
JP NZ,TDKBO
VISP:LD A,000
LD (VIPS+1),A
PUSH HL
LD HL,VIBTA
LD A,L
VIAD:ADD 000
LD L,A
LD A,(VIAD+1)
INC A
AND 7
LD (VIAD+1),A
LD B,(HL)
LD A,(SMCC+1)
ADD B
LD (SMCC+1),A
POP HL
TDKBO:LD A,(INOU)
CP 0
JP Z,FIN
CP 2
JP Z,ND4
SISP:LD A,000
DEC A
LD (SISP+1),A
JP NZ,ND4
LD A,(ATTAK)
LD (SISP+1),A
ROC1:LD B,000
COR1:LD A,(SMXD+1)
INC A
CPD:CP 000
JP Z,ND1
LD (SMXD+1),A
LD A,(SDS+1)
DEC A
LD (SDS+1),A
ND1:LD A,(SMXE+1)
INC A
CPE:CP 000
JP Z,ND2
LD (SMXE+1),A
LD A,(SES+1)
DEC A
LD (SES+1),A
ND2:LD A,(SMXH+1)
INC A
CPH:CP 000
JP Z,FLIP
LD (SMXH+1),A
LD A,(SHS+1)
DEC A
LD (SHS+1),A
DJNZ COR1
ND4:LD A,000
XOR 1
LD (ND4+1),A
JP Z,MOOD
EX AF,AF
JP POT
MOOD:EX AF,AF
DEC A
JP NZ,POT
JP STAR

BASIC:LD IY,23610
LD BC,65533
LD A,7
OUT (C),A
LD BC,49149
LD A,63
OUT (C),A
EI 
RET 



SORT:LD A,(INOM)
AND A
JP Z,XIF
LD A,B
LD (SMCC+1),A
SRL A
SRL A
LD B,A
SRL A
SRL A
ADD B
LD (SCS+1),A
LD (CPC+1),A
LD A,1
LD (SMXC+1),A
JP IVA
XIF:LD A,B
LD (SMCC+1),A
SRL A
SRL A
LD B,A
SRL A
SRL A
ADD B
LD (SMXC+1),A
LD (CPC+1),A
LD A,1
LD (SCS+1),A
IVA:LD A,000
LD (VIB+1),A
XOR A
LD (VIAD+1),A
RET 

FLIP:XOR A
LD (INOU),A
JP ND4

FLIM:XOR A
LD (INOM),A
JP AMP

FINIT:LD A,D
LD (SMCD+1),A
SRL A
SRL A
SRL A
LD (SMXD+1),A
LD A,E
LD (SMCE+1),A
SRL A
SRL A
SRL A
LD (SMXE+1),A
LD A,H
LD (SMCH+1),A
SRL A
SRL A
SRL A
LD (SMXH+1),A
LD A,1
LD (SDS+1),A
LD (SES+1),A
LD (SHS+1),A
JP IRT

FIN:LD A,000
DEC A
LD (FIN+1),A
JP NZ,ND4
LD A,(DECAY)
LD (FIN+1),A
ROC2:LD B,000
COR2:LD A,(SMXD+1)
DEC A
JP Z,ND5
LD (SMXD+1),A
LD A,(SDS+1)
INC A
LD (SDS+1),A
ND5:LD A,(SMXE+1)
DEC A
JP Z,ND6
LD (SMXE+1),A
LD A,(SES+1)
INC A
LD (SES+1),A
ND6:LD A,(SMXH+1)
DEC A
SUS:CP 000
JP Z,THIT
LD (SMXH+1),A
LD A,(SHS+1)
INC A
LD (SHS+1),A
DJNZ COR2
JP ND4

THIT:LD A,2
LD (INOU),A
JP ND4

BASS:LD BC,700
BAD:DEC BC
LD A,B
OR C
JP NZ,BAD
LD A,24
OUT (254),A
LD A,200
BAD3:DEC A
JP NZ,BAD3
LD A,210
LD (KOP+1),A
RET 

SNAR:LD HL,0
LD B,10
SN1:LD A,24
OUT (254),A
LD A,(HL)
INC HL
AND 128
ADD 16
SN2:DEC A
JP NZ,SN2
XOR A
OUT (254),A
LD A,20
SN3:DEC A
JP NZ,SN3
DJNZ SN1
RET 

HIGH:LD BC,65533
LD A,13
OUT (C),A
LD BC,49149
LD A,1
OUT (C),A
LD C,5
LD HL,10
HIN:LD A,24
OUT (254),A
LD B,(HL)
INC HL
HIEL:DJNZ HIEL
XOR A
OUT (254),A
LD B,(HL)
INC HL
HEIR:DJNZ HEIR
PUSH BC
LD BC,120
HINE:DEC BC
LD A,B
OR C
JP NZ,HINE
POP BC
DEC C
JP NZ,HIN
LD A,240
LD (KOP+1),A
RET 

TINK:LD C,30
LD HL,1000
TIN:LD A,24
OUT (254),A
LD B,C
TIEL:DJNZ TIEL
XOR A
OUT (254),A
LD A,31
SUB C
LD B,A
TEIR:DJNZ TEIR
LD A,(HL)
AND 16
LD B,A
INC HL
TILE:DJNZ TILE
DEC C
JP NZ,TIN
RET 

FONK:LD HL,2000
LD C,25
SO1:LD A,24
OUT (254),A
LD B,30
SO2:DJNZ SO2
XOR A
OUT (254),A
LD A,(HL)
INC HL
AND 128
LD B,A
SO3:DJNZ SO3
DEC C
JP NZ,SO1
LD A,150
LD (KOP+1),A
RET 

SMCL:LD L,000
HELP:LD A,000
INC A
AND 3
LD (HELP+1),A
JP NZ,L4
LD A,24
OUT (254),A
SMMCL:LD A,000
D4:DEC A
JP NZ,D4
XOR A
OUT (254),A
SMFL:LD A,001
DILL:DEC A
JP NZ,DILL
JP L4

KOP:LD B,000
XOR A
LD (KOP+1),A
LD A,0F7H
IN A,(254)
AND 1
JR Z,TOP
CALL TOP
CALL TOP
CALL TOP
CALL TOP
INC C
TOP:DEC C
JR Z,SMCC
LB:DEC D
JR Z,SMCD
L1:DEC E
JR Z,SMCE
L2:DEC H
JR Z,SMCH
L3:DEC L
JR Z,SMCL
L4:NOP
NOP
DJNZ TOP
RET 

SMCH:LD H,000
LD A,24
OUT (254),A
SMXH:LD A,000
D3:DEC A
JP NZ,D3
XOR A
OUT (254),A
SHS:LD A,000
MU3:DEC A
JP NZ,MU3
JP L3

SMCE:LD E,000
LD A,24
OUT (254),A
SMXE:LD A,000
D2:DEC A
JP NZ,D2
XOR A
OUT (254),A
SES:LD A,000
MU2:DEC A
JP NZ,MU2
JP L2
SMCD:LD D,000
LD A,24
OUT (254),A
SMXD:LD A,000
D1:DEC A
JP NZ,D1
XOR A
OUT (254),A
SDS:LD A,000
MU1:DEC A
JP NZ,MU1
JP L1
SMCC:LD C,000
LD A,24
OUT (254),A
SMXC:LD A,000
DD3:DEC A
JP NZ,DD3
XOR A
OUT (254),A
SCS:LD A,000
MU4:DEC A
JP NZ,MU4
JP LB

INOU:DB 0
ININ:DB 0
ATTAK:DB 20
DECAY:DB 20
INOM:DB 0
INIM:DB 0
ATTAM:DB 1
DECAM:DB 15
DB 0
HLST:DW 0
MEREP:DB 0,0,0
BAREP:DB 0,0,0
CHREP:DB 0,0,0,255

TAB1:DB 58,0,3,3,2,3,1,3
DB 255,255

TAB2:DB 8,0,8,3,4,0,4,0
DB 2,3,4,0,2,5
DB 8,0,2,3,6,0
DB 4,0,4,0
DB 4,3,2,0,2,0
DB 8,0,2,3,4,0,2,5
DB 4,0,2,0,2,0
DB 2,3,2,0,4,0
DB 8,0,8,3,4,0,2,0
DB 4,3,2,3,4,0,255,255

TAB3:DB 3,0,2,0,1,0
DB 3,3,2,0,1,0
DB 2,0,1,0,2,0,1,0
DB 3,3,2,0,1,0
DB 3,0,2,0,1,0
DB 2,3,1,0,2,0,1,0
DB 2,3,1,0,2,0,1,3
DB 3,3,2,0,1,3
DB 3,0,2,0,1,0
DB 3,3,2,0,1,3
DB 2,0,1,0,2,0,1,0
DB 3,0,2,0,1,0
DB 255,255

TAB4:DB 1,3,1,3,2,3,2,3,2,3
DB 255,255

TABLE:DB 7,21,8,16,9,16,10,16
DB 11,0,12,11,6,8,2,80
DB 3,66,0,0

VIBTA:DB 2,1,255,254,254,255
DB 1,2

ECTA:DS 64

END
