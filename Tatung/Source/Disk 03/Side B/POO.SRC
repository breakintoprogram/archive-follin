;��������������������������������������
;�  ��  �   �   ��  ��  � � � ��� ��  �
;� �  �  � �    � � � � � � � �   � � �
;� ����   �     � � ��  � � � ��  ��  �
;� �  �   �     � � � � � � � �   � � �
;� �  �   �     ��  � � �  �  ��� � � �
;�                                    �
;�  �    LAST UPDATED: 21/12/87�      �
;��������������������������������������

ORG 50000
LOAD 49152

KEY 4 '|\A|M|MO|M'
KEY 7 '|\SPACKRAT|M'

TOP:XOR A
OUT (254),A
LD HL,DATA1
LD (DATAA+1),HL
LD HL,DATA2
LD (DATAB+1),HL
LD HL,DATA3
LD (DATAC+1),HL
LD A,1
LD (CHANA+1),A
LD (CHANB+1),A
LD (CHANC+1),A
LD (EFO+1),A
RET
SPLAT:
LD A,2
OUT (254),A

CHANA:LD A,000
DEC A
LD (CHANA+1),A
JP NZ,EFO
DATAA:LD HL,000
DATR:LD A,(HL)
INC HL
AND A
JR Z,TOP
CP 1
JR NZ,NEA1
AREM:LD A,000
DEC A
JR Z,DATR
LD (AREM+1),A
LD HL,(ARST)
JP DATR
NEA1:CP 2
JR NZ,NEA2
LD A,(HL)
INC HL
LD (AREM+1),A
LD (ARST),HL
JP DATR
NEA2:CP 3
JR NZ,NEA3
LD BC,65533
LD A,8
OUT (C),A
LD BC,49149
LD A,(HL)
OUT (C),A
LD (AENV+1),A
LD (AVOL),A
LD (A%ENV%),A
INC HL
LD A,(HL)
LD (AENA+1),A
LD (A%ENA%+1),A
INC HL
LD A,(HL)
LD (AEND+1),A
LD (A%END%+1),A
LD (ATAGE+1),A
INC HL
LD A,(HL)
LD (AENS+1),A
INC HL
LD A,(HL)
LD (GATE+1),A
INC HL
JP DATR
NEA3:CP 4
JR NZ,NEA4
XOR A
LD (AEINI+1),A
JP DATR
NEA4:CP 5
JR NZ,NEA5
LD A,1
LD (AEINI+1),A
JP DATR
NEA5:CP 6
JR NZ,NEA6
LD A,(HL)
LD (VIB+1),A
LD (DELAY+1),A
INC HL
JP DATR
NEA6:CP 9
JP NZ,NEA7
LD A,(HL)
LD (NEA9+1),A
INC HL
JP DATR
NEA7:CP 10
JP NZ,NEA8
LD E,(HL)
INC HL
LD D,(HL)
INC HL
LD (GAST),HL
PUSH DE
POP HL
JP DATR
NEA8:CP 11
JP NZ,NEA10
LD HL,(GAST)
JP DATR
NEA10:CP 12
JP NZ,NEA9
LD A,(HL)
LD (FREQ+1),A
INC HL
JP DATR
NEA9:ADD 000
LD (TRY3+1),A
LD E,A
LD A,(HL)
LD (CHANA+1),A
INC HL
LD (DATAA+1),HL
LD D,0
LD HL,NOTES
ADD HL,DE
ADD HL,DE
FREQ:LD A,000
DEC A
JP Z,TRY2
LD BC,65533
XOR A
OUT (C),A
LD BC,49149
LD A,(HL)
OUT (C),A
LD BC,65533
LD A,1
OUT (C),A
LD BC,49149
INC HL
LD A,(HL)
LD (APITCH+1),A
OUT (C),A
JP AEINI
TRY2:LD BC,65533
XOR A
OUT (C),A
LD BC,49149
XOR A
OUT (C),A
LD BC,65533
LD A,1
OUT (C),A
LD BC,49149
TRY3:LD A,000
LD (APITCH+1),A
OUT (C),A
AEINI:LD A,000
DEC A
JR NZ,EFO
LD A,(A%ENV%)
LD (AVOL),A
LD (AENV+1),A
LD A,(A%END%+1)
LD (ATAGE+1),A
DELAY:LD A,000
LD (VIB+1),A
EFO:LD A,001
DEC A
JP Z,CHANB
LD (EFO+1),A
DEC A
JP NZ,CHANB
LD BC,65533
LD A,7
OUT (C),A
LD BC,49149
LD A,(GTST)
OUT (C),A
CHANB:LD A,000
DEC A
LD (CHANB+1),A
JP NZ,CHANC
DATAB:LD HL,000
DBTR:LD A,(HL)
INC HL
AND A
JP Z,TOP
CP 1
JR NZ,NEB1
BREM:LD A,000
DEC A
JR Z,DBTR
LD (BREM+1),A
LD HL,(BRST)
JP DBTR
NEB1:CP 2
JR NZ,NEB2
LD A,(HL)
INC HL
LD (BREM+1),A
LD (BRST),HL
JP DBTR
NEB2:CP 3
JR NZ,NEB3
LD BC,65533
LD A,9
OUT (C),A
LD BC,49149
LD A,(HL)
OUT (C),A
LD (BENV+1),A
LD (BVOL),A
LD (B%ENV%),A
INC HL
LD A,(HL)
LD (BENA+1),A
LD (B%ENA%+1),A
INC HL
LD A,(HL)
LD (BEND+1),A
LD (B%END%+1),A
INC HL
LD A,(HL)
LD (BENS+1),A
INC HL
JP DBTR
NEB3:CP 4
JR NZ,NEB4
XOR A
LD (BEINI+1),A
JP DBTR
NEB4:CP 5
JR NZ,NEB5
LD A,1
LD (BEINI+1),A
JP DBTR
NEB5:CP 7
JR NZ,NEB6
LD BC,65533
LD A,7
OUT (C),A
LD BC,49149
LD A,(HL)
LD (GTST),A
INC HL
OUT (C),A
LD BC,65533
LD A,6
OUT (C),A
LD BC,49149
LD A,(HL)
INC HL
OUT (C),A
JP DBTR
NEB6:CP 8
JP NZ,NEB7
LD A,(HL)
INC HL
INC A
LD (EFO+1),A
LD (EFOI),A
JP DBTR
NEB7:CP 9
JP NZ,NEB8
LD A,(HL)
LD (NEB10+1),A
INC HL
JP DBTR
NEB8:CP 10
JP NZ,NEB9
LD E,(HL)
INC HL
LD D,(HL)
INC HL
LD (GBST),HL
PUSH DE
POP HL
JP DBTR
NEB9:CP 11
JP NZ,NEB10
LD HL,(GBST)
JP DBTR
NEB10:ADD 000
LD E,A
LD A,(HL)
LD (CHANB+1),A
INC HL
LD (DATAB+1),HL
LD D,0
LD HL,NOTES
ADD HL,DE
ADD HL,DE
LD BC,65533
LD A,3
OUT (C),A
LD BC,49149
LD A,(HL)
OUT (C),A
LD BC,65533
LD A,2
OUT (C),A
LD BC,49149
INC HL
LD A,(HL)
OUT (C),A
LD A,(EFOI)
LD (EFO+1),A
DEC A
JP Z,BEINI
LD BC,65533
LD A,7
OUT (C),A
LD A,(GTST)
SUB 32
LD BC,49149
OUT (C),A
BEINI:LD A,000
DEC A
JR NZ,CHANC
LD A,(B%ENV%)
LD (BVOL),A
LD (BENV+1),A
CHANC:LD A,000
DEC A
LD (CHANC+1),A
JP NZ,AENV
DATAC:LD HL,000
DCTR:LD A,(HL)
INC HL
AND A
JP Z,TOP
CP 1
JR NZ,NEC1
CREM:LD A,000
DEC A
JR Z,DCTR
LD (CREM+1),A
LD HL,(CRST)
JP DCTR
NEC1:CP 2
JR NZ,NEC2
LD A,(HL)
INC HL
LD (CREM+1),A
LD (CRST),HL
JP DCTR
NEC2:CP 3
JR NZ,NEC3
LD BC,65533
LD A,10
OUT (C),A
LD BC,49149
LD A,(HL)
OUT (C),A
LD (CENV+1),A
LD (CVOL),A
LD (C%ENV%),A
INC HL
LD A,(HL)
LD (CENA+1),A
LD (C%ENA%+1),A
INC HL
LD A,(HL)
LD (CEND+1),A
LD (C%END%+1),A
INC HL
LD A,(HL)
LD (CENS+1),A
INC HL
JP DCTR
NEC3:CP 4
JR NZ,NEC4
XOR A
LD (CEINI+1),A
JP DCTR
NEC4:CP 5
JR NZ,NEC5
LD A,1
LD (CEINI+1),A
JP DCTR
NEC5:CP 9
JP NZ,NEC6
LD A,(HL)
LD (NEC8+1),A
INC HL
JP DCTR
NEC6:CP 10
JP NZ,NEC7
LD E,(HL)
INC HL
LD D,(HL)
INC HL
LD (GCST),HL
PUSH DE
POP HL
JP DCTR
NEC7:CP 11
JP NZ,NEC8
LD HL,(GCST)
JP DCTR
NEC8:ADD 000
LD E,A
LD A,(HL)
LD (CHANC+1),A
INC HL
LD (DATAC+1),HL
LD D,0
LD HL,NOTES
ADD HL,DE
ADD HL,DE
LD BC,65533
LD A,5
OUT (C),A
LD BC,49149
LD A,(HL)
OUT (C),A
LD BC,65533
LD A,4
OUT (C),A
LD BC,49149
INC HL
LD A,(HL)
OUT (C),A
CEINI:LD A,000
DEC A
JR NZ,AENV
LD A,(C%ENV%)
LD (CVOL),A
LD (CENV+1),A
AENV:LD A,000
CP 13
JP Z,AEND
AENA:LD A,000
DEC A
LD (AENA+1),A
JR NZ,GATE
A%ENA%:LD A,000
LD (AENA+1),A
LD BC,65533
LD A,8
OUT (C),A
LD BC,49149
LD A,(AVOL)
INC A
LD (AVOL),A
LD (AENV+1),A
OUT (C),A
JP GATE
AEND:LD A,000
DEC A
LD (AEND+1),A
JR NZ,GATE
A%END%:LD A,000
LD (AEND+1),A
ATAGE:LD A,000
LD (AEND+1),A
LD BC,65533
LD A,8
OUT (C),A
LD BC,49149
LD A,(AVOL)
DEC A
OUT (C),A
AENS:CP 000
JP Z,GATE
LD (AVOL),A
GATE:LD A,000
AND A
JR Z,BENV
LD B,A
LD A,(CHANA+1)
CP B
JR NZ,BENV
LD A,13
LD (AENV+1),A
LD A,1
LD (AEND+1),A
LD (ATAGE+1),A
BENV:LD A,000
CP 13
JP Z,BEND
BENA:LD A,000
DEC A
LD (BENA+1),A
JP NZ,CENV
B%ENA%:LD A,000
LD (BENA+1),A
LD BC,65533
LD A,9
OUT (C),A
LD BC,49149
LD A,(BVOL)
INC A
LD (BVOL),A
LD (BENV+1),A
OUT (C),A
JP CENV
BEND:LD A,000
DEC A
LD (BEND+1),A
JP NZ,CENV
B%END%:LD A,000
LD (BEND+1),A
LD BC,65533
LD A,9
OUT (C),A
LD BC,49149
LD A,(BVOL)
DEC A
OUT (C),A
BENS:CP 000
JR Z,CENV
LD (BVOL),A
CENV:LD A,000
CP 13
JP Z,CEND
CENA:LD A,000
DEC A
LD (CENA+1),A
JP NZ,VIB
C%ENA%:LD A,000
LD (CENA+1),A
LD BC,65533
LD A,10
OUT (C),A
LD BC,49149
LD A,(CVOL)
INC A
OUT (C),A
LD (CVOL),A
LD (CENV+1),A
JP VIB
CEND:LD A,000
DEC A
LD (CEND+1),A
JP NZ,VIB
C%END%:LD A,000
LD (CEND+1),A
LD BC,65533
LD A,10
OUT (C),A
LD BC,49149
LD A,(CVOL)
DEC A
OUT (C),A
CENS:CP 000
JR Z,VIB
LD (CVOL),A
VIB:LD A,000
DEC A
LD (VIB+1),A
JR NZ,CHEN
INC A
LD (VIB+1),A
LD HL,TABLE
BIV1:LD A,000
INC A
AND 7
LD (BIV1+1),A
LD E,A
LD D,0
ADD HL,DE
LD A,(HL)
APITCH:ADD 000
LD D,A
LD BC,65533
XOR A
OUT (C),A
LD BC,49149
LD A,D
OUT (C),A
CHEN:XOR A
OUT (254),A
RET

TABLE:DB 3,1,0-1,0-3,0-3,0-1,1,3

AVOL:DB 0
BVOL:DB 0
CVOL:DB 0

ARST:DW 0
BRST:DW 0
CRST:DW 0

A%ENV%:DB 0
B%ENV%:DB 0
C%ENV%:DB 0

GTST:DB 0
EFOI:DB 0
GAST:DW 0
GBST:DW 0
GCST:DW 0

NOTES:EQU $-16
DBH 0EEE0E180D4D0C8E0BDA
DBH 0B2F0A8F09F7096808E1
DBH 086107E90777070C06A7
DBH 064705ED0598054704FC
DBH 04D40470043103F403DC
DBH 03860353032402F602CC
DBH 02A4027E025A02380218
DBH 01FA01DE01C301AA0192
DBH 017B01660152013F012D
DBH 011C010C00FD00EF00E1
DBH 00D500C900BE00B300A9
DBH 009F0096008E0086007F
DBH 00770071006A0064005F
DBH 005900540050004B0047
DBH 0043003F003C00380035
DBH 0032002F002D002A0028
DBH 0026002400220020001E
DBH 001C001B001900180016
DBH 00150014001300120011
DBH 0010

STOP:EQU 0
NEXT:EQU 1
FOR:EQU 2
ADSR:EQU 3
EOF:EQU 4
EON:EQU 5
VIBRA:EQU 6     ;A ONLY
ONOI:EQU 7     ;B ONLY
EFFECT:EQU 8     ;B ONLY
TRANS:EQU 9
GOSUB:EQU 10
RETURN:EQU 11
FREQS:EQU 12

;______________________________________

DATA1:DB ADSR,0,0,0,0,0,EON
DB VIBRA,0,TRANS,0
;         DB   50,128
DB ADSR,12,1,2,0,0,EON
DB FREQS,1
DB 82,1,83,1,84,1,FREQS,0

DB 85,16,85,16,82,8,82,16
DB 85,16,85,16,85,8,82,16
DB 82,16+200
;______________________________________

DATA2:DB ADSR,11,1,4,0,EON
DB ADSR,0,0,0,0
DB ONOI,56,15,EFFECT,1
DB TRANS,0

DB FOR,4
DB 30,24,42,16,37,16,39,8
DB NEXT
;______________________________________

DATA3:DB ADSR,11,1,4,0,EON
DB ADSR,0,0,0,0
DB TRANS,256-12

DB FOR,8
DB 30,24,42,16,37,16,39,8
DB NEXT
;______________________________________


;======================================








;======================================

END
