;¬´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´¾
;¸********** A VERY DODGY ***********¸
;¸                                   ¸
;¸ ßß  ß   ß  àà  ààà  à à à ààà ààà ¸
;¸ß  ß  ß ß   à à à  à à à à à   à  à¸
;¸ßßßß   ß    à à ààà  à à à àà  ààà ¸
;¸ß  ß   ß    à à à  à à à à à   à  à¸
;¸ß  ß   ß    àà  à  à à  à  ààà à  à¸
;¸                                   ¸
;ñ´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´Ñ
;¸´´´´´´´´´(C)1988 Tim Follin´´´´´´´´Ñ
;ñ´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´Ñ
;¸ BBBBBB     OOO    DDDDD   Y     Y ¸
;¸ B     B   O   O   D    D   Y   Y  ¸
;¸ B     B  O     O  D     D   Y Y   ¸
;¸ BBBBBB   O     O  D     D    Y    ¸
;¸ B     B  O     O  D     D    Y    ¸
;¸ B     B  O     O  D     D    Y    ¸
;¸ B     B   O   O   D    D     Y    ¸
;¸ BBBBBB     OOO    DDDDD      Y    ¸
;¸                                   ¸
;¸ LAST UPDATED:9/2/88 ¸
;¸                                   ¸
;¼´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´®

                ORG  50000
                LOAD 49152

                CALL TOP

LOOP:           LD   A,0F7H
                IN   A,(254)
                AND  1
                JP   Z,SPEED
                HALT 
SPEED:          CALL CHANA
HOLD:           LD   A,0F7H
                IN   A,(254)
                AND  4
                JP   Z,HOLD
                LD   A,07FH
                IN   A,(254)
                AND  1
                JP   NZ,LOOP

                LD   A,7
                LD   C,63
                CALL SEND
                RET  

TOP:            LD   HL,GAME_A
                LD   (DATAA+1),HL
                LD   HL,GAME_B
                LD   (DATAB+1),HL
                LD   HL,GAME_C
                LD   (DATAC+1),HL
                LD   A,1
                LD   (CHANA+1),A
                LD   (CHANB+1),A
                LD   (CHANC+1),A
                LD   (EFO+1),A
                RET  

CHANA:          LD   A,0
                DEC  A
                LD   (CHANA+1),A
                JP   NZ,EFO
DATAA:          LD   HL,0
DATR:           LD   A,(HL)
                INC  HL
                AND  A
                JR   Z,TOP
                CP   1
                JR   NZ,NEA1
AREM:           LD   A,0
                DEC  A
                JR   Z,DATR
                LD   (AREM+1),A
                LD   HL,(ARST)
                JP   DATR
NEA1:           CP   2
                JR   NZ,NEA2
                LD   A,(HL)
                INC  HL
                LD   (AREM+1),A
                LD   (ARST),HL
                JP   DATR
NEA2:           CP   3
                JR   NZ,NEA3
                LD   A,8
                LD   C,(HL)
                CALL SEND
                LD   A,C
                LD   (AENV+1),A
                LD   (AVOL),A
                LD   (A%ENV%),A
                INC  HL
                LD   A,(HL)
                LD   (AENA+1),A
                LD   (A%ENA%+1),A
                INC  HL
                LD   A,(HL)
                LD   (AEND+1),A
                LD   (A%END%+1),A
                LD   (ATAGE+1),A
                INC  HL
                LD   A,(HL)
                LD   (AENS+1),A
                INC  HL
                LD   A,(HL)
                LD   (GATE+1),A
                INC  HL
                JP   DATR
NEA3:           CP   4
                JR   NZ,NEA4
                XOR  A
                LD   (AEINI+1),A
                JP   DATR
NEA4:           CP   5
                JR   NZ,NEA5
                LD   A,1
                LD   (AEINI+1),A
                JP   DATR
NEA5:           CP   6
                JR   NZ,NEA6
                LD   A,(HL)
                LD   (VIB+1),A
                LD   (DELAY+1),A
                INC  HL
                JP   DATR
NEA6:           CP   7
                JR   NZ,NEAR6
                LD   A,(HL)
                LD   (GTST),A
                LD   C,A
                LD   A,7
                CALL SEND
                INC  HL
                LD   A,(HL)
                LD   (NODE),A
                LD   C,A
                LD   A,6
                CALL SEND
                INC  HL
                XOR  A
                LD   (NOSP+1),A
                JP   DATR
NEAR6:          CP   9
                JP   NZ,NEA7
                LD   A,(HL)
                LD   (NEA9+1),A
                INC  HL
                JP   DATR
NEA7:           CP   10
                JP   NZ,NEA8
                LD   E,(HL)
                INC  HL
                LD   D,(HL)
                INC  HL
                LD   (GAST),HL
                PUSH DE
                POP  HL
                JP   DATR
NEA8:           CP   11
                JP   NZ,NEA10
                LD   HL,(GAST)
                JP   DATR
NEA10:          CP   15
                JP   NZ,NEA11
                LD   A,(HL)
                INC  HL
                LD   (WOBM+1),A
                LD   (WOBR+1),A
                LD   A,(HL)
                INC  HL
                LD   (WEX+1),A
                JP   DATR
NEA11:          CP   16
                JP   NZ,NEA9
                LD   A,(HL)
                INC  HL
                LD   (LEAL+1),A
                JP   DATR
NEA9:           ADD  A,0
IAD:            ADD  A,0
                SUB  3
                LD   E,A
                LD   (WOBP+1),A
                XOR  A
                LD   (WOX+1),A
LEAL:           LD   A,000
                AND  A
                JP   NZ,LEOK
                LD   A,(HL)
                INC  HL
LEOK:           LD   (CHANA+1),A
                LD   (DATAA+1),HL
                LD   D,0
                LD   HL,NOTES
                ADD  HL,DE
                ADD  HL,DE
                LD   A,1
                LD   C,(HL)
                CALL SEND
                INC  HL
                LD   A,(HL)
                LD   (APITCH+1),A
                LD   C,A
                XOR  A
                CALL SEND
DELAY:          LD   A,0
                LD   (VIB+1),A
AEINI:          LD   A,0
                DEC  A
                JR   NZ,EFO
                LD   A,(A%ENV%)
                LD   (AVOL),A
                LD   (AENV+1),A
                LD   A,(A%END%+1)
                LD   (ATAGE+1),A
                XOR  A
                LD   (IAD+1),A
EFO:            LD   A,1
                DEC  A
                JP   Z,CHANB
                LD   (EFO+1),A
                DEC  A
                JP   NZ,CHANB
                LD   A,(GTST)
                LD   C,A
                LD   A,7
                CALL SEND
CHANB:          LD   A,0
                DEC  A
                LD   (CHANB+1),A
                JP   NZ,CHANC
DATAB:          LD   HL,0
DBTR:           LD   A,(HL)
                INC  HL
                AND  A
                JP   Z,TOP
                CP   1
                JR   NZ,NEB1
BREM:           LD   A,0
                DEC  A
                JR   Z,DBTR
                LD   (BREM+1),A
                LD   HL,(BRST)
                JP   DBTR
NEB1:           CP   2
                JR   NZ,NEB2
                LD   A,(HL)
                INC  HL
                LD   (BREM+1),A
                LD   (BRST),HL
                JP   DBTR
NEB2:           CP   3
                JR   NZ,NEB3
                LD   A,(HL)
                LD   (BENV+1),A
                LD   (BVOL),A
                LD   (B%ENV%),A
                LD   C,A
                LD   A,9
                CALL SEND
                INC  HL
                LD   A,(HL)
                LD   (BENA+1),A
                LD   (B%ENA%+1),A
                INC  HL
                LD   A,(HL)
                LD   (BEND+1),A
                LD   (B%END%+1),A
                INC  HL
                LD   A,(HL)
                LD   (BENS+1),A
                INC  HL
                JP   DBTR
NEB3:           CP   4
                JR   NZ,NEB4
                XOR  A
                LD   (BEINI+1),A
                JP   DBTR
NEB4:           CP   5
                JR   NZ,NEB5
                LD   A,1
                LD   (BEINI+1),A
                JP   DBTR
NEB5:           CP   7
                JR   NZ,NEB6
                LD   A,(HL)
                LD   (GTST),A
                LD   C,A
                LD   A,7
                CALL SEND
                INC  HL
                LD   A,(HL)
                LD   (NODE),A
                LD   C,A
                LD   A,6
                CALL SEND
                INC  HL
                XOR  A
                LD   (NOSP+1),A
                JP   DBTR
NEB6:           CP   8
                JP   NZ,NEB7
                LD   A,(HL)
                INC  HL
                INC  A
                LD   (EFO+1),A
                LD   (EFOI),A
                JP   DBTR
NEB7:           CP   9
                JP   NZ,NEB8
                LD   A,(HL)
                LD   (NEB10+1),A
                INC  HL
                JP   DBTR
NEB8:           CP   10
                JP   NZ,NEB9
                LD   E,(HL)
                INC  HL
                LD   D,(HL)
                INC  HL
                LD   (GBST),HL
                PUSH DE
                POP  HL
                JP   DBTR
NEB9:           CP   11
                JP   NZ,NEB11
                LD   HL,(GBST)
                JP   DBTR
NEB11:          CP   15
                JP   NZ,NEB12
                LD   A,(HL)
                INC  HL
                LD   (BOBM+1),A
                LD   (BOBR+1),A
                LD   A,(HL)
                INC  HL
                LD   (BEX+1),A
                JP   DBTR
NEB12:          CP   16
                JP   NZ,NEB10
                LD   A,(HL)
                INC  HL
                LD   (LEBL+1),A
                JP   DBTR
NEB10:          ADD  A,0
                SUB  3
                LD   E,A
                LD   (BOBP+1),A
                XOR  A
                LD   (BOX+1),A
LEBL:           LD   A,000
                AND  A
                JP   NZ,LEBK
                LD   A,(HL)
                INC  HL
LEBK:           LD   (CHANB+1),A
                LD   (DATAB+1),HL
                LD   D,0
                LD   HL,NOTES
                ADD  HL,DE
                ADD  HL,DE
                LD   A,3
                LD   C,(HL)
                CALL SEND
                LD   A,2
                INC  HL
                LD   C,(HL)
                CALL SEND
                LD   A,(EFOI)
                LD   (EFO+1),A
                DEC  A
                JP   Z,BEINI
                LD   A,(GTST)
                SUB  16
                LD   C,A
                LD   A,7
                CALL SEND
BEINI:          LD   A,0
                DEC  A
                JR   NZ,CHANC
                LD   A,(B%ENV%)
                LD   (BVOL),A
                LD   (BENV+1),A
CHANC:          LD   A,0
                DEC  A
                LD   (CHANC+1),A
                JP   NZ,AENV
DATAC:          LD   HL,0
DCTR:           LD   A,(HL)
                INC  HL
                AND  A
                JP   Z,TOP
                CP   1
                JR   NZ,NEC1
CREM:           LD   A,0
                DEC  A
                JR   Z,DCTR
                LD   (CREM+1),A
                LD   HL,(CRST)
                JP   DCTR
NEC1:           CP   2
                JR   NZ,NEC2
                LD   A,(HL)
                INC  HL
                LD   (CREM+1),A
                LD   (CRST),HL
                JP   DCTR
NEC2:           CP   3
                JR   NZ,NEC3
                LD   A,10
                LD   C,(HL)
                CALL SEND
                LD   A,C
                LD   (CENV+1),A
                LD   (CVOL),A
                LD   (C%ENV%),A
                INC  HL
                LD   A,(HL)
                LD   (CENA+1),A
                LD   (C%ENA%+1),A
                INC  HL
                LD   A,(HL)
                LD   (CEND+1),A
                LD   (C%END%+1),A
                INC  HL
                LD   A,(HL)
                LD   (CENS+1),A
                INC  HL
                JP   DCTR
NEC3:           CP   4
                JR   NZ,NEC4
                XOR  A
                LD   (CEINI+1),A
                JP   DCTR
NEC4:           CP   5
                JR   NZ,NEC5
                LD   A,1
                LD   (CEINI+1),A
                JP   DCTR
NEC5:           CP   7
                JR   NZ,NEC90
                LD   A,(HL)
                LD   (GTST),A
                LD   C,A
                LD   A,7
                CALL SEND
                INC  HL
                LD   A,(HL)
                LD   (NODE),A
                LD   C,A
                LD   A,6
                CALL SEND
                INC  HL
                XOR  A
                LD   (NOSP+1),A
                JP   DCTR
NEC90:          CP   9
                JP   NZ,NEC6
                LD   A,(HL)
                LD   (NEC8+1),A
                INC  HL
                JP   DCTR
NEC6:           CP   10
                JP   NZ,NEC7
                LD   E,(HL)
                INC  HL
                LD   D,(HL)
                INC  HL
                LD   (GCST),HL
                PUSH DE
                POP  HL
                JP   DCTR
NEC7:           CP   11
                JP   NZ,NEC9
                LD   HL,(GCST)
                JP   DCTR
NEC9:           CP   12
                JP   NZ,NEC91
                LD   A,(HL)
                INC  HL
                LD   (CHEN+1),A
                LD   (NOSR+1),A
                LD   A,30
                LD   (NODE),A
                LD   A,1
                LD   (NOSP+1),A
                JP   DCTR
NEC91:          CP   13
                JP   NZ,NEC92
                LD   A,(NEC8+1)
                LD   (CANC+1),A
                JP   DCTR
NEC92:          CP   14
                JP   NZ,NEC11
                XOR  A
                LD   (CANC+1),A
                JP   DCTR
NEC11:          CP   15
                JP   NZ,NEC12
                LD   A,(HL)
                INC  HL
                LD   (COBM+1),A
                LD   (COBR+1),A
                LD   A,(HL)
                INC  HL
                LD   (CEX+1),A
                JP   DCTR
NEC12:          CP   16
                JP   NZ,NEC8
                LD   A,(HL)
                INC  HL
                LD   (LECL+1),A
                JP   DCTR
NEC8:           ADD  A,0
CANC:           SUB  0
                SUB  3
                LD   E,A
                LD   (COBP+1),A
                XOR  A
                LD   (COX+1),A
LECL:           LD   A,000
                AND  A
                JP   NZ,LECK
                LD   A,(HL)
                INC  HL
LECK:           LD   (CHANC+1),A
                LD   (DATAC+1),HL
                LD   D,0
                LD   HL,NOTES
                ADD  HL,DE
                ADD  HL,DE
                LD   A,5
                LD   C,(HL)
                CALL SEND
                LD   A,4
                INC  HL
                LD   C,(HL)
                CALL SEND
CEINI:          LD   A,0
                DEC  A
                JR   NZ,AENV
                LD   A,(C%ENV%)
                LD   (CVOL),A
                LD   (CENV+1),A
AENV:           LD   A,0
                CP   13
                JP   Z,AEND
AENA:           LD   A,0
                DEC  A
                LD   (AENA+1),A
                JR   NZ,GATE
A%ENA%:         LD   A,0
                LD   (AENA+1),A
                LD   A,(AVOL)
                INC  A
                LD   (AVOL),A
                LD   (AENV+1),A
                LD   C,A
                LD   A,8
                CALL SEND
                JP   GATE
AEND:           LD   A,0
                DEC  A
                LD   (AEND+1),A
                JR   NZ,GATE
A%END%:         LD   A,0
                LD   (AEND+1),A
ATAGE:          LD   A,0
                LD   (AEND+1),A
                LD   A,(AVOL)
                DEC  A
                LD   C,A
                LD   A,8
                CALL SEND
                LD   A,C
AENS:           CP   0
                JP   Z,GATE
                LD   (AVOL),A
GATE:           LD   A,0
                AND  A
                JR   Z,BENV
                LD   B,A
                LD   A,(CHANA+1)
                CP   B
                JR   NZ,BENV
                LD   A,13
                LD   (AENV+1),A
                LD   A,1
                LD   (AEND+1),A
                LD   (ATAGE+1),A
BENV:           LD   A,0
                CP   13
                JP   Z,BEND
BENA:           LD   A,0
                DEC  A
                LD   (BENA+1),A
                JP   NZ,CENV
B%ENA%:         LD   A,0
                LD   (BENA+1),A
                LD   A,(BVOL)
                INC  A
                LD   (BVOL),A
                LD   (BENV+1),A
                LD   C,A
                LD   A,9
                CALL SEND
                JP   CENV
BEND:           LD   A,0
                DEC  A
                LD   (BEND+1),A
                JP   NZ,CENV
B%END%:         LD   A,0
                LD   (BEND+1),A
                LD   A,(BVOL)
                DEC  A
                LD   C,A
                LD   A,9
                CALL SEND
                LD   A,C
BENS:           CP   0
                JR   Z,CENV
                LD   (BVOL),A
CENV:           LD   A,0
                CP   13
                JP   Z,CEND
CENA:           LD   A,0
                DEC  A
                LD   (CENA+1),A
                JP   NZ,VIB
C%ENA%:         LD   A,0
                LD   (CENA+1),A
                LD   A,(CVOL)
                INC  A
                LD   (CVOL),A
                LD   (CENV+1),A
                LD   C,A
                LD   A,10
                CALL SEND
                JP   VIB
CEND:           LD   A,0
                DEC  A
                LD   (CEND+1),A
                JP   NZ,VIB
C%END%:         LD   A,0
                LD   (CEND+1),A
                LD   A,(CVOL)
                DEC  A
                LD   C,A
                LD   A,10
                CALL SEND
                LD   A,C
CENS:           CP   0
                JR   Z,VIB
                LD   (CVOL),A
VIB:            LD   A,0
                DEC  A
                LD   (VIB+1),A
                JR   NZ,CHEN
                INC  A
                LD   (VIB+1),A
                LD   HL,TTABLE
BIV1:           LD   A,0
                INC  A
                AND  7
                LD   (BIV1+1),A
                LD   E,A
                LD   D,0
                ADD  HL,DE
                LD   A,(HL)
APITCH:         ADD  A,0
                LD   D,A
                XOR  A
                LD   C,D
                CALL SEND
CHEN:           LD   A,0
                CP   255
                JP   Z,WOBM
                DEC  A
                LD   (CHEN+1),A
                JP   NZ,WOBM
NOSR:           LD   A,0
                LD   (CHEN+1),A
NOSP:           LD   A,0
                DEC  A
                JP   Z,NOSU
                LD   A,(NODE)
                INC  A
                LD   (NODE),A
                CP   29
                JP   NZ,NSET
                LD   A,(NOSP+1)
                XOR  1
                LD   (NOSP+1),A
                LD   A,31
                JP   NSET
NOSU:           LD   A,(NODE)
                DEC  A
                LD   (NODE),A
                CP   5
                JP   NZ,NSET
                LD   A,(NOSP+1)
                XOR  1
                LD   (NOSP+1),A
NSET:           LD   D,A
                LD   A,6
                LD   C,D
                CALL SEND
WOBM:           LD   A,0
                AND  A
                JP   Z,BOBM
                DEC  A
                LD   (WOBM+1),A
                JP   NZ,BOBM
WOBR:           LD   A,0
                LD   (WOBM+1),A
WOX:            LD   A,0
WEX:            XOR  0
                LD   (WOX+1),A
                LD   E,A
WOBP:           LD   A,0
                ADD  A,E
                LD   E,A
                LD   D,0
                LD   HL,NOTES
                ADD  HL,DE
                ADD  HL,DE
                LD   A,1
                LD   C,(HL)
                CALL SEND
                XOR  A
                INC  HL
                LD   C,(HL)
                CALL SEND
BOBM:           LD   A,0
                AND  A
                JP   Z,COBM
                DEC  A
                LD   (BOBM+1),A
                JP   NZ,COBM
BOBR:           LD   A,0
                LD   (BOBM+1),A
BOX:            LD   A,0
BEX:            XOR  0
                LD   (BOX+1),A
                LD   E,A
BOBP:           LD   A,0
                ADD  A,E
                LD   E,A
                LD   D,0
                LD   HL,NOTES
                ADD  HL,DE
                ADD  HL,DE
                LD   A,3
                LD   C,(HL)
                CALL SEND
                LD   A,2
                INC  HL
                LD   C,(HL)
                CALL SEND
COBM:           LD   A,0
                AND  A
                JP   Z,COME
                DEC  A
                LD   (COBM+1),A
                JP   NZ,COME
COBR:           LD   A,0
                LD   (COBM+1),A
COX:            LD   A,0
CEX:            XOR  0
                LD   (COX+1),A
                LD   E,A
COBP:           LD   A,0
                ADD  A,E
                LD   E,A
                LD   D,0
                LD   HL,NOTES
                ADD  HL,DE
                ADD  HL,DE
                LD   A,5
                LD   C,(HL)
                CALL SEND
                LD   A,4
                INC  HL
                LD   C,(HL)
                CALL SEND
COME:           RET  

SEND:           PUSH BC
                PUSH BC
                LD   BC,65533
                OUT  (C),A
                POP  BC
                LD   A,C
                LD   BC,49149
                OUT  (C),A
                POP  BC
                RET  

TTABLE:         DB   0-5,0-3,0,3,5,3,0,0-3

AVOL:           NOP  
BVOL:           NOP  
CVOL:           NOP  

ARST:           DW   0
BRST:           DW   0
CRST:           DW   0

A%ENV%:         NOP  
B%ENV%:         NOP  
C%ENV%:         NOP  

GTST:           NOP  
EFOI:           NOP  
NODE:           NOP  
GAST:           DW   0
GBST:           DW   0
GCST:           DW   0

NOTES:          EQU  !-16
                DBH  0EEE0E180D4D0C8E0BDA
                DBH  0B2F0A8F09F7096808E1
                DBH  086107E90777070C06A7
                DBH  064705ED0598054704FC
                DBH  04B40470043103F403BC
                DBH  03860353032402F602CC
                DBH  02A4027E025A02380218
                DBH  01FA01DE01C301AA0192
                DBH  017B01660152013F012D
                DBH  011C010C00FD00EF00E1
                DBH  00D500C900BE00B300A9
                DBH  009F0096008E0086007F
                DBH  00770071006A0064005F
                DBH  005900540050004B0047
                DBH  0043003F003C00380035
                DBH  0032002F002D002A0028
                DBH  0026002400220020001E
                DBH  001C001B001900180016
                DBH  00150014001300120011
                DBH  00100010001000100010

STOP:           EQU  0
NEXT:           EQU  1
FOR:            EQU  2
ADSR:           EQU  3
EOF:            EQU  4
EON:            EQU  5
VIBRA:          EQU  6
ONOI:           EQU  7
EFFECT:         EQU  8
TRANS:          EQU  9
GOSUB:          EQU  10
GOTO:           EQU  10
RETURN:         EQU  11
NOISE:          EQU  12
CANON:          EQU  13
CANOF:          EQU  14
WOBBLE:         EQU  15
LENGTH:         EQU  16

;=====================================
;GAME TUNE
;=====================================

GAME_A:         DB   ADSR,12,1,0,1,3,EOF
                DB   VIBRA,16,60,144
                DB   60,143,EON
                DB   72,99,77,9,71,3,72,6
                DB   70,9,68,9,65,9,70,4
                DB   68,5,65,63
                DB   ADSR,0,0,0,0,0,65,9
                DB   ADSR,10,1,1,10,0
                DB   64,9,67,9,70,9
                DB   75,18,73,18,71,2,72,7
                DB   65,18,65,18,65,18,65,9
                DB   71,3,72,6,71,9,75,3
                DB   76,6,75,9,78,3,79,6
                DB   78,9,83,3,84,6,82,9
                DB   80,3,82,3,80,3,77,63
                DB   65,5,64,4,68,5,67,4
                DB   72,5,71,4,77,5,76,4
                DB   80,5,79,4,80,5,82,4
                DB   83,5,84,4,86,5,87,4

                DB   77,108,77,9,78,9,77,9
                DB   75,9,77,5,75,4,73,5
                DB   72,4,70,72,77,9,78,9
                DB   77,9,75,9,73,9,72,9
                DB   70,18,77,9
                DB   65,18
                DB   68,18,69,9,70,9
                DB   77,18,65,9
                DB   72,18,71,18,70,9,69,9
                DB   70,9,65,18,65,9,67,9
                DB   69,9,70,9,77,18,77,18
                DB   77,9,89,4,89,15
;ÚØúøÚØúøÚØúøÚØúøÚØúøÚØúøÚØúøÚØúøÚØúøÚ

REST_A:         DB   WOBBLE,3,7
                DB   VIBRA,0
                DB   58,14,58,14,58,14,58,14
                DB   58,8,60,8
                DB   58,14,58,14,58,14,58,14
                DB   58,8,60,8
                DB   61,14,61,14,61,14,61,14
                DB   61,8,60,8
                DB   58,14,58,14,58,14,58,14
                DB   58,8,60,8

                DB   WOBBLE,0,0
                DB   VIBRA,30

                DB   41,1,53,98
                DB   51,9,56,9
                DB   53,9,51,9,49,9,48,18
                DB   44,90,46,9,48,9,49,9
                DB   53,9,54,108,56,9
                DB   58,9,59,9,61,9,63,14
                DB   54,87,63,9,61,9,59,9
                DB   58,9,59,9,61,54,56,9
                DB   61,45,59,9,58,9,56,9
                DB   58,9,59,54,54,9,59,45
                DB   58,9,56,9,54,9,56,9
                DB   58,54,53,9,58,63,60,18
                DB   63,71
                DB   50,1
                DB   WOBBLE,2,16,99,72
                DB   WOBBLE,0,0

                DB   55,3,56,42,52,3,53,6
                DB   49,5,44,49,44,4,45,5
                DB   46,4,47,5,48,4,49,5
                DB   50,4,51,5,51,5,49,4
                DB   44,5,49,4,54,5,52,4
                DB   58,5,56,4,65,18,63,14
                DB   64,4,68,5,65,4,61,5
                DB   56,4,63,5,63,4
                DB   63,5,62,4,60,5,56,4
                DB   51,5,52,4,49,5,48,4
                DB   44,5,45,4

                DB   46,5,44,4,39,5,37,4
                DB   39,27,37,9,42,9,39,4
                DB   39,5,46,5
                DB   46,4
                DB   45,5,46,4,51,5,49,4
                DB   50,5,54,4,51,5,53,4
                DB   54,5,55,4,57,5,58,4
                DB   59,5,61,4,63,36,61,12
                DB   58,12,54,12
                DB   51,6,56,6,49,6,54,6
                DB   59,6,52,6,57,6,61,6
                DB   56,6,66,1
                DB   99,15

                DB   GOTO
                DW   REST_A
;.....................................

GAME_B:         DB   ADSR,12,1,6,0,EON
                DB   EFFECT,2
                DB   FOR,6
                DB   29,9,29,9,29,9,29,9
                DB   24,9,24,9,23,9
                DB   24,9
                DB   ADSR,0,0,0,0,24,9
                DB   ADSR,12,1,6,0
                DB   29,9,41,9,35,9
                DB   34,4,35,5,36,4,34,5
                DB   32,9,27,9,NEXT
                DB   FOR,3
                DB   34,9,34,9,34,9,34,9
                DB   29,9,29,9,28,9
                DB   29,9
                DB   ADSR,0,0,0,0,34,9
                DB   ADSR,12,1,6,0
                DB   34,9,41,9,37,9
                DB   36,4,37,5,39,4,37,5
                DB   36,9,32,9,NEXT
                DB   34,9,34,9,34,9,34,9
                DB   29,9,29,9,28,9
                DB   29,9
                DB   ADSR,0,0,0,0,34,9
                DB   ADSR,12,1,6,0
                DB   41,18,41,18
                DB   41,9,41,4,41,14
;ÚØúøÚØúøÚØúøÚØúøÚØúøÚØúøÚØúøÚØúøÚØúøÚ

REST_B:         DB   FOR,4
                DB   27,14,27,9,25,5
                DB   27,9,29,5,27,5
                DB   27,5,25,4,39,8,29,8
                DB   NEXT
                DB   FOR,4
                DB   27,14,27,14,27,14,27,5
                DB   27,5,25,4,39,8,29,8
                DB   NEXT
                DB   FOR,4
                DB   26,14,26,14,26,14,26,5
                DB   26,5,26,4,26,8,27,8
                DB   NEXT,LENGTH,9
                DB   FOR,2
                DB   25,25,28,25
                DB   31,25,30,23
                DB   NEXT
                DB   FOR,2
                DB   23,23,25,25
                DB   27,27,28,30
                DB   NEXT
                DB   22,34,22,34
                DB   24,36,24,36
                DB   25,37,25,37
                DB   26,38,26,38
                DB   FOR,4,27,39,NEXT
                DB   EFFECT,0
                DB   FOR,4,25,37,NEXT
                DB   EFFECT,2

                DB   LENGTH,0
                DB   FOR,4
                DB   27,14,27,10,27,4,22,8
                DB   27,18,34,18,NEXT
                DB   FOR,4
                DB   25,14,25,10,25,4,20,8
                DB   25,18,32,18,NEXT

                DB   GOTO
                DW   REST_B
;.....................................

GAME_C:         DB   ADSR,12,1,1,9,EON
                DB   ONOI,56,10
                DB   FOR,6
                DB   WOBBLE,0,0,53,18
                DB   WOBBLE,2,253,56,18
                DB   WOBBLE,2,252,60,18
                DB   WOBBLE,2,251,65,9
                DB   WOBBLE,2,6,59,9
                DB   WOBBLE,0,0,55,18
                DB   WOBBLE,2,253,58,18
                DB   WOBBLE,2,251,63,18
                DB   WOBBLE,2,2,61,9
                DB   WOBBLE,0,254,63,9
                DB   NEXT
                DB   FOR,4
                DB   WOBBLE,0,0,58,18
                DB   WOBBLE,2,253,61,18
                DB   WOBBLE,2,252,65,18
                DB   WOBBLE,2,1,64,9
                DB   WOBBLE,2,255,65,9
                DB   WOBBLE,0,0,60,18
                DB   WOBBLE,2,253,63,18
                DB   WOBBLE,2,253,66,18
                DB   WOBBLE,2,1,65,9
                DB   WOBBLE,2,255,66,9
                DB   NEXT
;ÚØúøÚØúøÚØúøÚØúøÚØúøÚØúøÚØúøÚØúøÚØúøÚ
                DB   TRANS,0

REST_C:         DB   WOBBLE,2,7
                DB   54,14,54,14,54,14,54,14
                DB   54,8,56,8
                DB   54,14,54,14,54,14,54,14
                DB   54,8,56,8
                DB   57,14,57,14,57,14,57,14
                DB   57,8,56,8
                DB   54,14,54,14,54,14,54,14
                DB   54,8,56,8
                DB   WOBBLE,1,7
                DB   FOR,4,EON
                DB   61,4,EOF,65,3,61,4,65,3
                DB   EON
                DB   61,4,EOF,65,3,61,4,65,3
                DB   EON
                DB   61,4,EOF,65,3,61,4,65,3
                DB   EON
                DB   61,4,EOF,65,3,61,4,65,3
                DB   LENGTH,4,EON
                DB   61,EOF,65,63,66,LENGTH,0
                DB   NEXT
                DB   WOBBLE,2,12
                DB   FOR,4,EON
                DB   62,4,EOF,65,3,70,4,73,3
                DB   EON
                DB   62,4,EOF,65,3,70,4,73,3
                DB   EON
                DB   62,4,EOF,65,3,70,4,73,3
                DB   EON
                DB   62,4,EOF,65,3,70,4,73,3
                DB   LENGTH,2,EON
                DB   62,EOF,65,70,73,63,66
                DB   70,73,LENGTH,0
                DB   NEXT
                DB   FOR,2,EON
                DB   61,4,EOF,64,3,68,4,73,3
                DB   EON
                DB   61,4,EOF,64,3,68,4,73,3
                DB   EON
                DB   61,4,EOF,64,3,68,4,73,3
                DB   EON
                DB   61,4,EOF,64,3,68,4,73,3
                DB   EOF,LENGTH,2
                DB   61,64,68,73,63,66,70,75
                DB   LENGTH,0
                DB   NEXT
                DB   FOR,2,EON
                DB   59,4,EOF,63,3,66,4,71,3
                DB   EON
                DB   59,4,EOF,63,3,66,4,71,3
                DB   EON
                DB   59,4,EOF,63,3,66,4,71,3
                DB   EON
                DB   59,4,EOF,63,3,66,4,71,3
                DB   EON,LENGTH,2
                DB   59,63,66,71,61,64,68,73
                DB   LENGTH,0
                DB   NEXT
                DB   FOR,2,EON
                DB   58,4,EOF,62,3,65,4,70,3
                DB   EON
                DB   58,4,EOF,62,3,65,4,70,3
                DB   EON
                DB   58,4,EOF,62,3,65,4,70,3
                DB   EON
                DB   58,4,EOF,62,3,65,4,70,3
                DB   EON,LENGTH,2
                DB   58,62,65,70,60,63,67,72
                DB   LENGTH,0
                DB   NEXT,EON
                DB   63,4,EOF,67,3,70,4,65,3
                DB   EON
                DB   63,4,EOF,67,3,70,4,65,3
                DB   EON
                DB   63,4,EOF,67,3,70,4,65,3
                DB   EON
                DB   63,4,EOF,67,3,70,4,65,3
                DB   EON,LENGTH,2
                DB   63,67,70,65,65,66,72,67
                DB   LENGTH,0
                DB   EON
                DB   59,3,EOF,63,3,66,3,70,3
                DB   EON
                DB   59,3,EOF,63,3,66,3,70,3
                DB   EON
                DB   59,3,EOF,63,3,66,3,71,3
                DB   NEXT
                DB   FOR,4
                DB   GOSUB
                DW   FOSEL
                DB   NEXT,TRANS,3
                DB   FOR,4
                DB   GOSUB
                DW   FOSEL
                DB   NEXT

                DB   GOTO
                DW   REST_C
;.....................................

FOSEL:          DB   EON
                DB   61,3,EOF,65,3,68,3,63,3
                DB   61,3,65,3,EON
                DB   61,3,EOF,65,3,68,3,63,3
                DB   61,3,65,3,EON
                DB   61,3,EOF,65,3,68,3,63,3
                DB   61,3,65,3,EON
                DB   60,3,EOF,63,3,67,3,62,3
                DB   63,3,67,3
                DB   RETURN

                END  
