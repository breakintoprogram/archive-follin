;¬´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´¾
;¸********** A VERY DODGY ***********¸
;¸                                   ¸
;¸ ßß  ß   ß  àà  ààà  à à à ààà ààà ¸
;¸ß  ß  ß ß   à à à  à à à à à   à  à¸
;¸ßßßß   ß    à à ààà  à à à àà  ààà ¸
;¸ß  ß   ß    à à à  à à à à à   à  à¸
;¸ß  ß   ß    àà  à  à à  à  ààà à  à¸
;¸                                   ¸
;ñ´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´Ñ
;¸´´´´´´´´´(C)1988 Tim Follin´´´´´´´´Ñ
;ñ´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´Ñ
;¸ LAST UPDATED:1900 BC ¸
;ñ´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´Ñ
;¸                                   ¸
;¸BRRRING BRRRING!  BRRRING! BRRRING!¸
;¸ -click!-                          ¸
;¸                                   ¸
;¸ "Hello? Hello?"                   ¸
;¸                                   ¸
;¸ "First, I'm going to lie you down ¸
;¸on the BED! Then I'm going to get a¸
;¸warm spoon and I'm going to smother¸
;¸you in avocado puree and LICK IT   ¸
;¸OFF!! Slowly!! THEN I'm going to   ¸
;¸get out the LEATHER  STRAPS and..."¸
;¸                                   ¸
;¸ "Hello? Hello? Which service do   ¸
;¸you require? Police, Ambulance or  ¸
;¸Fire? Hello?"                      ¸
;¸                                   ¸
;¸ -click!-                          ¸
;¸                                   ¸
;¸BRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRR   ¸
;¸                                   ¸
;¸                                   ¸
;¸-Approved by the spider of warning.¸
;¸                                   ¸
;¸ THIS HAS BEEN A "Weirdness at     ¸
;¸  lunchtime" PRODUCTION.           ¸
;¸                                   ¸
;¼´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´´®

                ORG  50000
                LOAD 49152

                CALL TOP

LOOP:           LD   A,0F7H
                IN   A,(254)
                AND  1
                JP   Z,SPEED
                HALT 
                HALT 
SPEED:          CALL CHANA
                CALL CHANA
HOLD:           LD   A,0F7H
                IN   A,(254)
                AND  4
                JP   Z,HOLD
                LD   A,07FH
                IN   A,(254)
                AND  1
                JP   NZ,LOOP

                LD   A,7
                LD   C,63
                CALL SEND
                RET  

TOP:            LD   HL,GAME_A
                LD   (DATAA+1),HL
                LD   HL,GAME_B
                LD   (DATAB+1),HL
                LD   HL,GAME_C
                LD   (DATAC+1),HL
                LD   A,1
                LD   (CHANA+1),A
                LD   (CHANB+1),A
                LD   (CHANC+1),A
                LD   (EFO+1),A
                RET  

CHANA:          LD   A,0
                DEC  A
                LD   (CHANA+1),A
                JP   NZ,EFO
DATAA:          LD   HL,0
DATR:           LD   A,(HL)
                INC  HL
                AND  A
                JR   Z,TOP
                CP   1
                JR   NZ,NEA1
AREM:           LD   A,0
                DEC  A
                JR   Z,DATR
                LD   (AREM+1),A
                LD   HL,(ARST)
                JP   DATR
NEA1:           CP   2
                JR   NZ,NEA2
                LD   A,(HL)
                INC  HL
                LD   (AREM+1),A
                LD   (ARST),HL
                JP   DATR
NEA2:           CP   3
                JR   NZ,NEA3
                LD   A,8
                LD   C,(HL)
                CALL SEND
                LD   A,C
                LD   (AENV+1),A
                LD   (AVOL),A
                LD   (A%ENV%),A
                INC  HL
                LD   A,(HL)
                LD   (AENA+1),A
                LD   (A%ENA%+1),A
                INC  HL
                LD   A,(HL)
                LD   (AEND+1),A
                LD   (A%END%+1),A
                LD   (ATAGE+1),A
                INC  HL
                LD   A,(HL)
                LD   (AENS+1),A
                INC  HL
                LD   A,(HL)
                LD   (GATE+1),A
                INC  HL
                JP   DATR
NEA3:           CP   4
                JR   NZ,NEA4
                XOR  A
                LD   (AEINI+1),A
                JP   DATR
NEA4:           CP   5
                JR   NZ,NEA5
                LD   A,1
                LD   (AEINI+1),A
                JP   DATR
NEA5:           CP   6
                JR   NZ,NEA6
                LD   A,(HL)
                LD   (VIB+1),A
                LD   (DELAY+1),A
                INC  HL
                JP   DATR
NEA6:           CP   7
                JR   NZ,NEAR6
                LD   A,(HL)
                LD   (GTST),A
                LD   C,A
                LD   A,7
                CALL SEND
                INC  HL
                LD   A,(HL)
                LD   (NODE),A
                LD   C,A
                LD   A,6
                CALL SEND
                INC  HL
                XOR  A
                LD   (NOSP+1),A
                JP   DATR
NEAR6:          CP   9
                JP   NZ,NEA7
                LD   A,(HL)
                LD   (NEA9+1),A
                INC  HL
                JP   DATR
NEA7:           CP   10
                JP   NZ,NEA8
                LD   E,(HL)
                INC  HL
                LD   D,(HL)
                INC  HL
                LD   (GAST),HL
                PUSH DE
                POP  HL
                JP   DATR
NEA8:           CP   11
                JP   NZ,NEA10
                LD   HL,(GAST)
                JP   DATR
NEA10:          CP   15
                JP   NZ,NEA11
                LD   A,(HL)
                INC  HL
                LD   (WOBM+1),A
                LD   (WOBR+1),A
                LD   A,(HL)
                INC  HL
                LD   (WEX+1),A
                JP   DATR
NEA11:          CP   16
                JP   NZ,NEA9
                LD   A,(HL)
                INC  HL
                LD   (LEAL+1),A
                JP   DATR
NEA9:           ADD  A,0
IAD:            ADD  A,0
                SUB  3
                LD   E,A
                LD   (WOBP+1),A
                XOR  A
                LD   (WOX+1),A
LEAL:           LD   A,000
                AND  A
                JP   NZ,LEOK
                LD   A,(HL)
                INC  HL
LEOK:           LD   (CHANA+1),A
                LD   (DATAA+1),HL
                LD   D,0
                LD   HL,NOTES
                ADD  HL,DE
                ADD  HL,DE
                LD   A,1
                LD   C,(HL)
                CALL SEND
                INC  HL
                LD   A,(HL)
                LD   (APITCH+1),A
                LD   C,A
                XOR  A
                CALL SEND
DELAY:          LD   A,0
                LD   (VIB+1),A
AEINI:          LD   A,0
                DEC  A
                JR   NZ,EFO
                LD   A,(A%ENV%)
                LD   (AVOL),A
                LD   (AENV+1),A
                LD   A,(A%END%+1)
                LD   (ATAGE+1),A
                XOR  A
                LD   (IAD+1),A
EFO:            LD   A,1
                DEC  A
                JP   Z,CHANB
                LD   (EFO+1),A
                DEC  A
                JP   NZ,CHANB
                LD   A,(GTST)
                LD   C,A
                LD   A,7
                CALL SEND
CHANB:          LD   A,0
                DEC  A
                LD   (CHANB+1),A
                JP   NZ,CHANC
DATAB:          LD   HL,0
DBTR:           LD   A,(HL)
                INC  HL
                AND  A
                JP   Z,TOP
                CP   1
                JR   NZ,NEB1
BREM:           LD   A,0
                DEC  A
                JR   Z,DBTR
                LD   (BREM+1),A
                LD   HL,(BRST)
                JP   DBTR
NEB1:           CP   2
                JR   NZ,NEB2
                LD   A,(HL)
                INC  HL
                LD   (BREM+1),A
                LD   (BRST),HL
                JP   DBTR
NEB2:           CP   3
                JR   NZ,NEB3
                LD   A,(HL)
                LD   (BENV+1),A
                LD   (BVOL),A
                LD   (B%ENV%),A
                LD   C,A
                LD   A,9
                CALL SEND
                INC  HL
                LD   A,(HL)
                LD   (BENA+1),A
                LD   (B%ENA%+1),A
                INC  HL
                LD   A,(HL)
                LD   (BEND+1),A
                LD   (B%END%+1),A
                INC  HL
                LD   A,(HL)
                LD   (BENS+1),A
                INC  HL
                JP   DBTR
NEB3:           CP   4
                JR   NZ,NEB4
                XOR  A
                LD   (BEINI+1),A
                JP   DBTR
NEB4:           CP   5
                JR   NZ,NEB5
                LD   A,1
                LD   (BEINI+1),A
                JP   DBTR
NEB5:           CP   7
                JR   NZ,NEB6
                LD   A,(HL)
                LD   (GTST),A
                LD   C,A
                LD   A,7
                CALL SEND
                INC  HL
                LD   A,(HL)
                LD   (NODE),A
                LD   C,A
                LD   A,6
                CALL SEND
                INC  HL
                XOR  A
                LD   (NOSP+1),A
                JP   DBTR
NEB6:           CP   8
                JP   NZ,NEB7
                LD   A,(HL)
                INC  HL
                INC  A
                LD   (EFO+1),A
                LD   (EFOI),A
                JP   DBTR
NEB7:           CP   9
                JP   NZ,NEB8
                LD   A,(HL)
                LD   (NEB10+1),A
                INC  HL
                JP   DBTR
NEB8:           CP   10
                JP   NZ,NEB9
                LD   E,(HL)
                INC  HL
                LD   D,(HL)
                INC  HL
                LD   (GBST),HL
                PUSH DE
                POP  HL
                JP   DBTR
NEB9:           CP   11
                JP   NZ,NEB11
                LD   HL,(GBST)
                JP   DBTR
NEB11:          CP   15
                JP   NZ,NEB12
                LD   A,(HL)
                INC  HL
                LD   (BOBM+1),A
                LD   (BOBR+1),A
                LD   A,(HL)
                INC  HL
                LD   (BEX+1),A
                JP   DBTR
NEB12:          CP   16
                JP   NZ,NEB10
                LD   A,(HL)
                INC  HL
                LD   (LEBL+1),A
                JP   DBTR
NEB10:          ADD  A,0
                SUB  3
                LD   E,A
                LD   (BOBP+1),A
                XOR  A
                LD   (BOX+1),A
LEBL:           LD   A,000
                AND  A
                JP   NZ,LEBK
                LD   A,(HL)
                INC  HL
LEBK:           LD   (CHANB+1),A
                LD   (DATAB+1),HL
                LD   D,0
                LD   HL,NOTES
                ADD  HL,DE
                ADD  HL,DE
                LD   A,3
                LD   C,(HL)
                CALL SEND
                LD   A,2
                INC  HL
                LD   C,(HL)
                CALL SEND
                LD   A,(EFOI)
                LD   (EFO+1),A
                DEC  A
                JP   Z,BEINI
                LD   A,(GTST)
                SUB  16
                LD   C,A
                LD   A,7
                CALL SEND
BEINI:          LD   A,0
                DEC  A
                JR   NZ,CHANC
                LD   A,(B%ENV%)
                LD   (BVOL),A
                LD   (BENV+1),A
CHANC:          LD   A,0
                DEC  A
                LD   (CHANC+1),A
                JP   NZ,AENV
DATAC:          LD   HL,0
DCTR:           LD   A,(HL)
                INC  HL
                AND  A
                JP   Z,TOP
                CP   1
                JR   NZ,NEC1
CREM:           LD   A,0
                DEC  A
                JR   Z,DCTR
                LD   (CREM+1),A
                LD   HL,(CRST)
                JP   DCTR
NEC1:           CP   2
                JR   NZ,NEC2
                LD   A,(HL)
                INC  HL
                LD   (CREM+1),A
                LD   (CRST),HL
                JP   DCTR
NEC2:           CP   3
                JR   NZ,NEC3
                LD   A,10
                LD   C,(HL)
                CALL SEND
                LD   A,C
                LD   (CENV+1),A
                LD   (CVOL),A
                LD   (C%ENV%),A
                INC  HL
                LD   A,(HL)
                LD   (CENA+1),A
                LD   (C%ENA%+1),A
                INC  HL
                LD   A,(HL)
                LD   (CEND+1),A
                LD   (C%END%+1),A
                INC  HL
                LD   A,(HL)
                LD   (CENS+1),A
                INC  HL
                JP   DCTR
NEC3:           CP   4
                JR   NZ,NEC4
                XOR  A
                LD   (CEINI+1),A
                JP   DCTR
NEC4:           CP   5
                JR   NZ,NEC5
                LD   A,1
                LD   (CEINI+1),A
                JP   DCTR
NEC5:           CP   7
                JR   NZ,NEC90
                LD   A,(HL)
                LD   (GTST),A
                LD   C,A
                LD   A,7
                CALL SEND
                INC  HL
                LD   A,(HL)
                LD   (NODE),A
                LD   C,A
                LD   A,6
                CALL SEND
                INC  HL
                XOR  A
                LD   (NOSP+1),A
                JP   DCTR
NEC90:          CP   9
                JP   NZ,NEC6
                LD   A,(HL)
                LD   (NEC8+1),A
                INC  HL
                JP   DCTR
NEC6:           CP   10
                JP   NZ,NEC7
                LD   E,(HL)
                INC  HL
                LD   D,(HL)
                INC  HL
                LD   (GCST),HL
                PUSH DE
                POP  HL
                JP   DCTR
NEC7:           CP   11
                JP   NZ,NEC9
                LD   HL,(GCST)
                JP   DCTR
NEC9:           CP   12
                JP   NZ,NEC91
                LD   A,(HL)
                INC  HL
                LD   (CHEN+1),A
                LD   (NOSR+1),A
                LD   A,30
                LD   (NODE),A
                LD   A,1
                LD   (NOSP+1),A
                JP   DCTR
NEC91:          CP   13
                JP   NZ,NEC92
                LD   A,(NEC8+1)
                LD   (CANC+1),A
                JP   DCTR
NEC92:          CP   14
                JP   NZ,NEC11
                XOR  A
                LD   (CANC+1),A
                JP   DCTR
NEC11:          CP   15
                JP   NZ,NEC12
                LD   A,(HL)
                INC  HL
                LD   (COBM+1),A
                LD   (COBR+1),A
                LD   A,(HL)
                INC  HL
                LD   (CEX+1),A
                JP   DCTR
NEC12:          CP   16
                JP   NZ,NEC8
                LD   A,(HL)
                INC  HL
                LD   (LECL+1),A
                JP   DCTR
NEC8:           ADD  A,0
CANC:           SUB  0
                SUB  3
                LD   E,A
                LD   (COBP+1),A
                XOR  A
                LD   (COX+1),A
LECL:           LD   A,000
                AND  A
                JP   NZ,LECK
                LD   A,(HL)
                INC  HL
LECK:           LD   (CHANC+1),A
                LD   (DATAC+1),HL
                LD   D,0
                LD   HL,NOTES
                ADD  HL,DE
                ADD  HL,DE
                LD   A,5
                LD   C,(HL)
                CALL SEND
                LD   A,4
                INC  HL
                LD   C,(HL)
                CALL SEND
CEINI:          LD   A,0
                DEC  A
                JR   NZ,AENV
                LD   A,(C%ENV%)
                LD   (CVOL),A
                LD   (CENV+1),A
AENV:           LD   A,0
                CP   13
                JP   Z,AEND
AENA:           LD   A,0
                DEC  A
                LD   (AENA+1),A
                JR   NZ,GATE
A%ENA%:         LD   A,0
                LD   (AENA+1),A
                LD   A,(AVOL)
                INC  A
                LD   (AVOL),A
                LD   (AENV+1),A
                LD   C,A
                LD   A,8
                CALL SEND
                JP   GATE
AEND:           LD   A,0
                DEC  A
                LD   (AEND+1),A
                JR   NZ,GATE
A%END%:         LD   A,0
                LD   (AEND+1),A
ATAGE:          LD   A,0
                LD   (AEND+1),A
                LD   A,(AVOL)
                DEC  A
                LD   C,A
                LD   A,8
                CALL SEND
                LD   A,C
AENS:           CP   0
                JP   Z,GATE
                LD   (AVOL),A
GATE:           LD   A,0
                AND  A
                JR   Z,BENV
                LD   B,A
                LD   A,(CHANA+1)
                CP   B
                JR   NZ,BENV
                LD   A,13
                LD   (AENV+1),A
                LD   A,1
                LD   (AEND+1),A
                LD   (ATAGE+1),A
BENV:           LD   A,0
                CP   13
                JP   Z,BEND
BENA:           LD   A,0
                DEC  A
                LD   (BENA+1),A
                JP   NZ,CENV
B%ENA%:         LD   A,0
                LD   (BENA+1),A
                LD   A,(BVOL)
                INC  A
                LD   (BVOL),A
                LD   (BENV+1),A
                LD   C,A
                LD   A,9
                CALL SEND
                JP   CENV
BEND:           LD   A,0
                DEC  A
                LD   (BEND+1),A
                JP   NZ,CENV
B%END%:         LD   A,0
                LD   (BEND+1),A
                LD   A,(BVOL)
                DEC  A
                LD   C,A
                LD   A,9
                CALL SEND
                LD   A,C
BENS:           CP   0
                JR   Z,CENV
                LD   (BVOL),A
CENV:           LD   A,0
                CP   13
                JP   Z,CEND
CENA:           LD   A,0
                DEC  A
                LD   (CENA+1),A
                JP   NZ,VIB
C%ENA%:         LD   A,0
                LD   (CENA+1),A
                LD   A,(CVOL)
                INC  A
                LD   (CVOL),A
                LD   (CENV+1),A
                LD   C,A
                LD   A,10
                CALL SEND
                JP   VIB
CEND:           LD   A,0
                DEC  A
                LD   (CEND+1),A
                JP   NZ,VIB
C%END%:         LD   A,0
                LD   (CEND+1),A
                LD   A,(CVOL)
                DEC  A
                LD   C,A
                LD   A,10
                CALL SEND
                LD   A,C
CENS:           CP   0
                JR   Z,VIB
                LD   (CVOL),A
VIB:            LD   A,0
                DEC  A
                LD   (VIB+1),A
                JR   NZ,CHEN
                INC  A
                LD   (VIB+1),A
                LD   HL,TTABLE
BIV1:           LD   A,0
                INC  A
                AND  7
                LD   (BIV1+1),A
                LD   E,A
                LD   D,0
                ADD  HL,DE
                LD   A,(HL)
APITCH:         ADD  A,0
                LD   D,A
                XOR  A
                LD   C,D
                CALL SEND
CHEN:           LD   A,0
                CP   255
                JP   Z,WOBM
                DEC  A
                LD   (CHEN+1),A
                JP   NZ,WOBM
NOSR:           LD   A,0
                LD   (CHEN+1),A
NOSP:           LD   A,0
                DEC  A
                JP   Z,NOSU
                LD   A,(NODE)
                INC  A
                LD   (NODE),A
                CP   29
                JP   NZ,NSET
                LD   A,(NOSP+1)
                XOR  1
                LD   (NOSP+1),A
                LD   A,31
                JP   NSET
NOSU:           LD   A,(NODE)
                DEC  A
                LD   (NODE),A
                CP   5
                JP   NZ,NSET
                LD   A,(NOSP+1)
                XOR  1
                LD   (NOSP+1),A
NSET:           LD   D,A
                LD   A,6
                LD   C,D
                CALL SEND
WOBM:           LD   A,0
                AND  A
                JP   Z,BOBM
                DEC  A
                LD   (WOBM+1),A
                JP   NZ,BOBM
WOBR:           LD   A,0
                LD   (WOBM+1),A
WOX:            LD   A,0
WEX:            XOR  0
                LD   (WOX+1),A
                LD   E,A
WOBP:           LD   A,0
                ADD  A,E
                LD   E,A
                LD   D,0
                LD   HL,NOTES
                ADD  HL,DE
                ADD  HL,DE
                LD   A,1
                LD   C,(HL)
                CALL SEND
                XOR  A
                INC  HL
                LD   C,(HL)
                CALL SEND
BOBM:           LD   A,0
                AND  A
                JP   Z,COBM
                DEC  A
                LD   (BOBM+1),A
                JP   NZ,COBM
BOBR:           LD   A,0
                LD   (BOBM+1),A
BOX:            LD   A,0
BEX:            XOR  0
                LD   (BOX+1),A
                LD   E,A
BOBP:           LD   A,0
                ADD  A,E
                LD   E,A
                LD   D,0
                LD   HL,NOTES
                ADD  HL,DE
                ADD  HL,DE
                LD   A,3
                LD   C,(HL)
                CALL SEND
                LD   A,2
                INC  HL
                LD   C,(HL)
                CALL SEND
COBM:           LD   A,0
                AND  A
                JP   Z,COME
                DEC  A
                LD   (COBM+1),A
                JP   NZ,COME
COBR:           LD   A,0
                LD   (COBM+1),A
COX:            LD   A,0
CEX:            XOR  0
                LD   (COX+1),A
                LD   E,A
COBP:           LD   A,0
                ADD  A,E
                LD   E,A
                LD   D,0
                LD   HL,NOTES
                ADD  HL,DE
                ADD  HL,DE
                LD   A,5
                LD   C,(HL)
                CALL SEND
                LD   A,4
                INC  HL
                LD   C,(HL)
                CALL SEND
COME:           RET  

SEND:           PUSH BC
                PUSH BC
                LD   BC,65533
                OUT  (C),A
                POP  BC
                LD   A,C
                LD   BC,49149
                OUT  (C),A
                POP  BC
                RET  

TTABLE:         DB   0-6,0-3,0,3,6,3,0,0-3

AVOL:           NOP  
BVOL:           NOP  
CVOL:           NOP  

ARST:           DW   0
BRST:           DW   0
CRST:           DW   0

A%ENV%:         NOP  
B%ENV%:         NOP  
C%ENV%:         NOP  

GTST:           NOP  
EFOI:           NOP  
NODE:           NOP  
GAST:           DW   0
GBST:           DW   0
GCST:           DW   0

NOTES:          EQU  !-16
                DBH  0EEE0E180D4D0C8E0BDA
                DBH  0B2F0A8F09F7096808E1
                DBH  086107E90777070C06A7
                DBH  064705ED0598054704FC
                DBH  04B40470043103F403BC
                DBH  03860353032402F602CC
                DBH  02A4027E025A02380218
                DBH  01FA01DE01C301AA0192
                DBH  017B01660152013F012D
                DBH  011C010C00FD00EF00E1
                DBH  00D500C900BE00B300A9
                DBH  009F0096008E0086007F
                DBH  00770071006A0064005F
                DBH  005900540050004B0047
                DBH  0043003F003C00380035
                DBH  0032002F002D002A0028
                DBH  0026002400220020001E
                DBH  001C001B001900180016
                DBH  00150014001300120011
                DBH  00100010001000100010

STOP:           EQU  0
NEXT:           EQU  1
FOR:            EQU  2
ADSR:           EQU  3
EOF:            EQU  4
EON:            EQU  5
VIBRA:          EQU  6
ONOI:           EQU  7
EFFECT:         EQU  8
TRANS:          EQU  9
GOSUB:          EQU  10
GOTO:           EQU  10
RETURN:         EQU  11
NOISE:          EQU  12
CANON:          EQU  13
CANOF:          EQU  14
WOBBLE:         EQU  15
LENGTH:         EQU  16

;=====================================
;GAME TUNE
;=====================================

GAME_A:         DB   ADSR,10,1,1,9,0,EON
                DB   20,192,25,48,TRANS,0
                DB   WOBBLE,3,12,VIBRA,0

                DB   56,96,54,36,56,12,52,6
                DB   51,6,49,156,51,24
                DB   52,6,54,6,56,60,59,24
                DB   57,72,61,12,59,6,57,6
                DB   59,6,57,6,54,60,57,24
                DB   57,24,56,24,54,24,56,24

                DB   WOBBLE,4,12
                DB   69,48,64,36,69,12,67,24
                DB   62,48,67,24
                DB   69,48,64,36,69,6,71,6
                DB   72,6,74,6,76,36
                DB   72,6,74,6,76,24,74,6
                DB   72,6,74,6,72,6,70,12
                DB   65,48,74,12,70,12
                DB   68,48,69,48,71,96
                DB   ADSR,10,1,0,1,3
                DB   WOBBLE,2,12,VIBRA,12
                DB   62,4,61,4,60,4,59,4
                DB   58,4,57,4,56,4,55,4
                DB   54,4,53,4,52,4,51,4
                DB   50,4,49,4,48,4,47,4
                DB   46,4,45,4,44,4,43,4
                DB   42,4,41,4,40,4,39,4
                DB   WOBBLE,0,0
                DB   FOR,2
                DB   GOSUB
                DW   RAND
                DB   TRANS,12,NEXT
                DB   TRANS,13,48,48
                DB   50,36,51,6,53,6,51,48
                DB   43,48,60,36,62,12
                DB   63,12,60,12,63,12,67,12
                DB   65,96
                DB   GOSUB
                DW   RAND
                DB   74,48,75,48,77,48,79,48
                DB   TRANS,12,FOR,3
                DB   23,6,25,6,37,6,25,6
                DB   28,6,23,6,22,6,20,6
                DB   NEXT,TRANS,24
                DB   23,6,25,6,37,6,25,6
                DB   28,6,23,6,22,6,20,5

                DB   STOP
;.....................................

GAME_B:         DB   ADSR,12,1,4,0,EON
                DB   EFFECT,2

                DB   FOR,9
                DB   25,12,25,6,25,6
                DB   25,12,25,6,25,6
                DB   25,12,25,6,25,6
                DB   25,6,25,6,25,6,25,6
                DB   NEXT
                DB   28,12,28,6,28,6
                DB   28,12,28,6,28,6
                DB   28,12,28,6,28,6
                DB   28,6,28,6,28,6,28,6

                DB   GOSUB
                DW   SQIG
                DB   TRANS,254,GOSUB
                DW   SQIG
                DB   TRANS,0,GOSUB
                DW   SQIG
                DB   TRANS,3,GOSUB
                DW   FRIG
                DB   TRANS,1,GOSUB
                DW   FRIG
                DB   TRANS,255,GOSUB
                DW   FRID
                DB   TRANS,252,GOSUB
                DW   FRID
                DB   TRANS,254,GOSUB
                DW   FRIG
                DB   GOSUB
                DW   FRIG

                DB   TRANS,0,FOR,16
                DB   22,6,24,6,36,6,24,6
                DB   27,6,22,6,21,6,19,6
                DB   NEXT
                DB   FOR,28
                DB   23,6,25,6,37,6,25,6
                DB   28,6,23,6,22,6,20,6
                DB   NEXT

                DB   STOP
;.....................................

GAME_C:         DB   ADSR,10,1,1,9,EON
                DB   ONOI,56,20,TRANS,0
                DB   NOISE,4
                DB   WOBBLE,3,12
                DB   FOR,2
                DB   25,12,25,6,25,6
                DB   25,12,25,6,25,6
                DB   25,12,25,6,25,6
                DB   25,12,25,6,25,6
                DB   NEXT
                DB   49,96,47,96,45,96,42,96
                DB   49,96,50,96,51,96,47,96

                DB   ADSR,9,0,0,0
                DB   WOBBLE,3,12
                DB   LENGTH,6
                DB   GOSUB
                DW   MIHA
                DB   TRANS,254,GOSUB
                DW   MIHA
                DB   TRANS,0,GOSUB
                DW   MIHA
                DB   TRANS,3,GOSUB
                DW   MAHA
                DB   TRANS,1,GOSUB
                DW   MAHA
                DB   TRANS,255,GOSUB
                DW   MAHD
                DB   TRANS,252,GOSUB
                DW   MAHD
                DB   TRANS,254,GOSUB
                DW   MAHA
                DB   TRANS,254,GOSUB
                DW   MAHA
                DB   TRANS,0
                DB   LENGTH,0
                DB   WOBBLE,2,7
                DB   GOSUB
                DW   RIPP
                DB   GOSUB
                DW   RIPP
                DB   WOBBLE,2,12
                DB   TRANS,1,FOR,16
                DB   60,4,63,4,67,4,NEXT
                DB   FOR,8
                DB   58,4,63,4,67,4,NEXT
                DB   FOR,8
                DB   57,4,60,4,65,4,NEXT
                DB   WOBBLE,2,19
                DB   GOSUB
                DW   RIPP
                DB   TRANS,13,WOBBLE,2,7
                DB   FOR,8
                DB   56,4,60,4,63,4,NEXT
                DB   WOBBLE,2,19
                DB   FOR,8
                DB   56,4,60,4,63,4,NEXT
                DB   TRANS,19,WOBBLE,0,0
                DB   FOR,8
                DB   23,6,25,6,37,6,25,6
                DB   28,6,23,6,22,6,20,6
                DB   NEXT

                DB   STOP
;.....................................

SQIG:           DB   21,12,21,6,21,6
                DB   24,12,21,6,21,6
SQID:           DB   26,6,21,6,21,6
                DB   28,6,21,6,26,6
                DB   24,6,19,6,RETURN

FRIG:           DB   21,12,21,6,21,6
                DB   25,12,21,6,21,6
FRID:           DB   26,6,21,6,21,6
                DB   28,6,21,6,21,6
                DB   25,6,19,6,RETURN

MIHA:           DB   57,60,64,69,72,69,65,64
MIHD:           DB   57,59,60,64,65,64,60,65
                DB   RETURN

MAHA:           DB   57,61,64,69,73,69,59,64
MAHD:           DB   57,59,61,64,59,64,61,59
                DB   RETURN

RIPP:           DB   FOR,8
                DB   60,4,63,4,67,4,NEXT
                DB   FOR,8
                DB   58,4,63,4,67,4,NEXT
                DB   FOR,8
                DB   57,4,60,4,65,4,NEXT
                DB   FOR,8
                DB   56,4,60,4,63,4,NEXT
                DB   RETURN

RAND:           DB   60,48
                DB   65,12
                DB   67,84,65,36,67,12
                DB   63,6,62,6,60,84
                DB   72,96
                DB   RETURN

                END  
